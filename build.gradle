buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath 'com.github.EnvyWare:PluginPublisher:-SNAPSHOT'
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: "com.github.dosmike.PluginPublisher"
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    group = 'com.envyful.api'
    version = '4.7.0'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    name = "envyware"
                    url = uri("https://maven.envyware.co.uk/releases")
                    credentials(PasswordCredentials)
                    authentication {
                        basic(BasicAuthentication)
                    }
                }
            }
            publications {
                maven(MavenPublication) {
                    groupId = "com.envyful.api"
                    artifactId = project.name
                    version = project.version
                    from components.java
                }
            }
        }
    }

    publish.dependsOn("javadoc")

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.lucko.me/' }
        maven { url = "https://repo.aikar.co/content/groups/aikar/" }
        maven { url = "https://jitpack.io" }
        maven {
        	name = 'sponge'
        	url = 'https://repo.spongepowered.org/maven'
    	}
    }
}
